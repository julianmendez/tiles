
class CcsInstanceBuilder

  abstract

  import
    soda.tiles.fairness.example.parser.YamlParser
    java.io.BufferedReader
    java.io.Reader

  agents_key = "agents"

  resources_key = "resources"

  outcome_key = "outcome"

  agent_children_key = "agent_children"

  agent_adults_key = "agent_adults"

  agent_income_key = "agent_income"

  resource_value_key = "resource_value"

  pipelines_key = "pipelines"

  to_measure (s : String) : Measure =
    s .toIntOption

  _get_agents (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Seq [Agent] =
    m .getOrElse (agents_key , None)
      .iterator
      .map (lambda pair --> pair ._1)
      .toSeq

  _get_resources (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Seq [Resource] =
    m .getOrElse (resources_key , None)
      .iterator
      .map (lambda pair --> pair ._1)
      .toSeq

  _get_outcome (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Outcome =
    Outcome .mk (
      m .getOrElse (outcome_key , None)
        .iterator
        .map(lambda pair --> Assignment .mk (pair._1) (pair ._2) )
        .toSeq
    )

  _get_agent_children_map (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Map [Agent] [Measure] =
    m .getOrElse (agent_children_key , None)
      .iterator
      .map (lambda pair --> Tuple2 (pair ._1 , to_measure (pair ._2) ) )
      .toMap

  _get_agent_adults_map (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Map [Agent] [Measure] =
    m .getOrElse (agent_adults_key , None)
      .iterator
      .map (lambda pair --> Tuple2 (pair ._1 , to_measure (pair ._2) ) )
      .toMap

  _get_agent_income_map (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Map [Agent] [Measure] =
    m .getOrElse (agent_income_key , None)
      .iterator
      .map (lambda pair --> Tuple2 (pair ._1 , to_measure (pair ._2) ) )
      .toMap

  _get_resource_value_map (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Map [Resource] [Measure] =
    m .getOrElse (resource_value_key , None)
      .iterator
      .map (lambda pair --> Tuple2 (pair ._1 , to_measure (pair ._2) ) )
      .toMap

  _get_pipelines (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Seq [String] =
    m .getOrElse (pipelines_key , None)
      .iterator
      .map (lambda pair --> pair ._1)
      .toSeq

  _build_from_map (m : Map [String] [Seq [Tuple2 [String] [String] ] ] )
      : Option [CcsInstance] =
    Some (
      CcsInstance .mk (
        _get_agents (m) ) (
        _get_resources (m) ) (
        _get_outcome (m) ) (
        _get_agent_children_map (m) ) (
        _get_agent_adults_map (m) ) (
        _get_agent_income_map (m) ) (
        _get_resource_value_map (m) ) (
        _get_pipelines (m)
      )
    )

  build (s : Seq [Seq [Tuple2 [String] [Seq [Tuple2 [String] [String] ] ] ] ] )
      : Option [CcsInstance] =
    match s
      case a +: as ==> _build_from_map (a .toMap)
      case otherwise ==> None

  from_yaml (reader : Reader) : Option [CcsInstance] =
     build (YamlParser .mk .parse (reader) )

end

