
directive lean
import Soda.tiles.fairness.tool.TileMessage


/**
 * This tile takes a sequence of actors as input and returns a sequence containing, for each
 * actor in the input sequence, a measure amounting the value of all the resources given that
 * actor. This tile requires a function to count multiple resources, and another function that
 * informs the value of each resource.
 */

class ReceivedSigmaPTile

  abstract
    sigma : Measure -> Measure -> Measure
    p : Resource -> Measure

  _sigma2 (m0 : Measure , m1 : Measure) : Measure =
    sigma (m0) (m1)

  _measure_zero : Measure = Some (0)

  get_assignment (assignments : Seq [Assignment] ) (a : Actor) : Option [Assignment] =
    assignments . find (lambda assignment --> (assignment .actor) == a)

  get_measure (outcome : Outcome) (a : Actor) : Measure =
    (get_assignment (outcome .assignments) (a) )
      .map (lambda assignment --> p (assignment .resource) )
      .foldLeft (_measure_zero) (_sigma2)

  apply (message : TileMessage [Seq [Actor] ] ) : TileMessage [Seq [Measure] ] =
    TileMessageBuilder .mk .build (message .context) (message .outcome) (
      ( (message .contents)
        .map (lambda actor --> get_measure (message .outcome) (actor) ) )
    )

end

